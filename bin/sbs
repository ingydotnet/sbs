#!/usr/bin/env ys-0
source <(curl '-sL' getys.org/run) "$@" :  # Run w/ bash support

fmt-11ty =: ENV.SBS_11TY.?

defn main(*files):
  when files.!:          die('No files specified')
  when (files.# % 2).?:  die('Specify an even number of files')
  each file files:
    when fs-f(file).! && (file != '_'):
      die("File '$file' does not exist")

  markdown =: |
    ### Side by Side File Comparisons

    _Generated by [`sbs`](
    https://github.com/ingydotnet/sbs/tree/main/bin/sbs) (a [YAMLScript](
    https://yamlscript.org/) program)._

  markdown +=:
    reduce _ '' partition(2 files):
      fn(md [left right]):
        ltext =:
          if left != '_': left:slurp:chomp "\n"
        rtext =:
          if right != '_': right:slurp:chomp "\n"
        lnum =: ltext:lines:count
        rnum =: rtext:lines:count
        ltext +=:
          or _ '':
            when lnum < rnum:
              ((rnum - lnum) * "\n") +
              fmt-11ty.if("\n" '')
        rtext +=:
          or _ '':
            when rnum < lnum:
              ((lnum - rnum) * "\n") +
              fmt-11ty.if("\n" '')

        raw-begin =: fmt-11ty && "{% raw %}"
        raw-end   =: fmt-11ty && "{% endraw %}"

        exts =::
          ys: yaml

        lext =: left:fs/extension
        lext =: exts.$lext || lext
        rext =: right:fs/extension
        rext =: exts.$rext || rext

        md +: |

          ----
          <table>
          <tr><td>

          **`$left`**

          </td><td>

          **`$right`**

          </td></tr>
          <tr><td>

          ```$lext
          $raw-begin$ltext$raw-end
          ```

          </td><td>

          ```$rext
          $raw-begin$rtext$raw-end
          ```

          </td></tr>
          </table>

  gist: markdown

defn gist(markdown):
  markdown-file =: '/tmp/side-by-side.md'
  spit markdown-file: markdown

  api-url =: 'https://api.github.com/gists'

  api-token-file =: ENV.GITHUB_API_TOKEN_FILE |||
    "$(ENV.HOME)/.github-api-token"
  api-key =: api-token-file:read:chomp

  request =::
    :headers:
      :Content-Type: application/json
      :Authorization:: "Bearer $api-key"
    :body::
      json/dump::
        :description: ''
        :public: false
        :files:
          ! markdown-file.replace(qr(".*/")):
            :content:: markdown

  response =:
    try:
      http/post api-url: request
      catch e:
        e =: ex-data(e)
        say:
          yaml/dump::
            status:: e.status
            body:: e.body:json/load
            api-key:: api-key
        exit: 1

  when-not 200 <= response.status <= 201:
    die: ("Gist request failed:\n\n" +
          json/load(response.body):yaml/dump)

  say: json/load(response.body).html_url

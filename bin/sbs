#!/usr/bin/env ys-0
source <(curl '-s' 'https://yamlscript.org/run-ys') "$@" :

defn main(*files):
  when files.!:          die('No files specified')
  when (files.# % 2).?:  die('Specify an even number of files')
  each file files:
    when-not fs-f(file): die("File '$file' does not exist")

  markdown =: |
    ### Side by Side File Comparisons

    _Generated by [`sbs`](
    https://github.com/ingydotnet/sbs/tree/main/bin/sbs) (a [YAMLScript](
    https://yamlscript.org/) program)._

  markdown +=:
    reduce _ '' partition(2 files):
      fn(md [left right]):
        ltext =: left:slurp:chomp
        rtext =: right:slurp:chomp
        lnum =: ltext:lines:count
        rnum =: rtext:lines:count
        ltext +=:
          or _ '':
            when lnum < rnum: (rnum - lnum).++ * "\n"
        rtext +=:
          or _ '':
            when rnum < lnum: (lnum - rnum).++ * "\n"

        md +: |

          ----
          <table>
          <tr><td>`$left`</td><td>`$right`</td></tr>
          <tr><td>

          ```$(left.replace(/.*\./))
          $ltext
          ```

          </td><td>

          ```$(right.replace(/.*\./))
          $rtext
          ```

          </td></tr>
          </table>

  gist: markdown

defn gist(markdown):
  markdown-file =: '/tmp/side-by-side.md'
  spit markdown-file: markdown

  api-url =: 'https://api.github.com/gists'

  api-key =: ENV.SBS_TOKEN ||
    die('No GitHub API token found in SBS_TOKEN')

  request =::
    :headers:
      :Content-Type: application/json
      :Authorization:: "Bearer $api-key"
    :body::
      json/dump::
        :description: ''
        :public: false
        :files:
          ! markdown-file.replace(qr(".*/")):
            :content:: markdown

  response =:
    try:
      http/post api-url: request
      catch e:
        e =: ex-data(e)
        say:
          yaml/dump::
            status:: e.status
            body:: e.body:json/load
            api-key:: api-key
        exit: 1

  when-not 200 <= response.status <= 201:
    die: ("Gist request failed:\n\n" +
          json/load(response.body):yaml/dump)

  say: json/load(response.body).html_url

